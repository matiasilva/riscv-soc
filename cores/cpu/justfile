HDL_DIR := "hdl/"
SRCS := "cpu_core.sv \
				insnmem.sv aluctrl.sv control.sv regfile.sv \
				alu.sv memory.sv \
				pipeline/q1q2.sv pipeline/q2q3.sv pipeline/q3q4.sv pipeline/q4q5.sv"
FULL_SRCS := prepend(HDL_DIR, SRCS)

# use slang to lint cpu_core
lint:
  slang {{FULL_SRCS}}

# run all cocotb tests
test filter="":
  uv run pytest -s -v {{ if filter == "" { filter } else { "-k " + filter } }}

# run tests, launch gtkwave
sim filter="": (test filter) waves

waves:
  surfer sim_build/*.fst

fixtures:
  $TOOLS_ROOT/gen_hex_data.py 128 -o $CPU_ROOT/tests/test_insnmem_preload_512.hex
  $TOOLS_ROOT/gen_hex_data.py 256 -o $CPU_ROOT/tests/test_insnmem_preload_1024.hex
  $TOOLS_ROOT/gen_hex_data.py 512 -o $CPU_ROOT/tests/test_insnmem_preload_2048.hex

