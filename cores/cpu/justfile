HDL_DIR := "hdl/"
SRCS := "cpu_core.sv \
				insnmem.sv aluctrl.sv control.sv regfile.sv \
				alu.sv memory.sv hazard_unit.sv forwarding_unit.sv \
				pipeline/p1p2.sv pipeline/p2p3.sv pipeline/p3p4.sv pipeline/p4p5.sv"
# INCLUDES := "cpu_types.vh"
FULL_SRCS := prepend(HDL_DIR, SRCS)
# FULL_INCLUDES := prepend("-I " + HDL_DIR, INCLUDES)

# use slang to lint cpu_core
lint:
  slang -Weverything -Wno-unused-typedef {{FULL_SRCS}} -I {{HDL_DIR}}

# run all cocotb tests
test filter="":
  uv run pytest -s -v {{ if filter == "" { filter } else { "-k " + filter } }}

# run tests, launch gtkwave
sim filter="": (test filter) waves

waves:
  surfer sim_build/*.fst

fixtures:
  $TOOLS_ROOT/gen_hex_data.py 128 -o $CPU_ROOT/tests/test_insnmem_preload_512.hex
  $TOOLS_ROOT/gen_hex_data.py 256 -o $CPU_ROOT/tests/test_insnmem_preload_1024.hex
  $TOOLS_ROOT/gen_hex_data.py 512 -o $CPU_ROOT/tests/test_insnmem_preload_2048.hex

draw:
  dot -Tsvg $CPU_ROOT/p1p2.gv -O
  dot -Tsvg $CPU_ROOT/p3p4p5.gv -O
